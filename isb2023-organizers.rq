#+ summary: A query for the ISB2023 organizing committee and scientific program committeee
#+ endpoint: https://query.wikidata.org/sparql
#+ tags:
#+   - conference
#+   - bibliometrics
#defaultView:Table

SELECT 
  ?person ?personLabel ?personDescription ?genderLabel ?image
  ?nationalityLabel ?orcid ?linkedin ?twitter ?gscholar ?github
  # ?affiliationLabel
  (GROUP_CONCAT(DISTINCT ?topic_label; separator=", ") AS ?topics)
WHERE 
{
  wd:Q111430238 wdt:P664|wdt:P5804 ?person .
  ?person wdt:P496 ?orcid .
  OPTIONAL { ?person wdt:P18 ?image . }
  OPTIONAL { ?person wdt:P21 ?gender . }
  OPTIONAL { ?person wdt:P2002 ?twitter . }
  OPTIONAL { ?person wdt:P27 ?nationality . }
  OPTIONAL { ?person wdt:P6634 ?linkedin . }
  OPTIONAL { ?person wdt:P1960 ?gscholar . }
  OPTIONAL { ?person wdt:P2037 ?github . }
  OPTIONAL { ?person wdt:P101 ?topic . ?topic rdfs:label ?topic_label . FILTER (LANG(?topic_label) = 'en') }
  # OPTIONAL { ?person wdt:P108|wdt:P1416 ?affiliation . }
  SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],en". } # Helps get the label in your language, if not, then en language
}
GROUP BY
  ?person ?personLabel ?personDescription ?genderLabel ?image
  ?nationalityLabel ?orcid ?linkedin ?twitter ?gscholar ?github
ORDER BY ?personLabel
